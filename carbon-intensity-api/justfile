alias t := test
alias lr := local-run
alias cr := compose-run
alias ld := load-test

REDIS_PORT := "7379"
REDIS_DOCKER_CONTAINER_NAME := "carbon-intensity-api-redis-standalone"

test:
    cargo nextest run

local-environment-shutdown:
    docker rm -f {{REDIS_DOCKER_CONTAINER_NAME}}

local-environment-startup: local-environment-shutdown
    docker run --name {{REDIS_DOCKER_CONTAINER_NAME}} -p {{REDIS_PORT}}:6379 -d redis:7.4.1-bookworm

# Meant for basic local testing
local-run: local-environment-startup
    APP__RATE_LIMITER__REDIS_SERVER__PORT={{REDIS_PORT}} APP__HTTP_SERVER__PORT=9999 cargo run

compose-build: 
    docker compose build

# runs the solution in a docker-compose deployment 
compose-run:
    docker compose up

# Runs a load test using k6 on the docker-compose deployment
load-test:
    docker run --rm -i --network="host" grafana/k6 run - <distributed_test.js